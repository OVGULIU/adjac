! -*-f90-*-
!
! adjac: Automatic Differentiation for generating Jacobians.
!

! Copyright (c) 2014, Pauli Virtanen <pav@iki.fi>
! All rights reserved.
!
! Redistribution and use in source and binary forms, with or without
! modification, are permitted provided that the following conditions
! are met:
!
! 1. Redistributions of source code must retain the above copyright
! notice, this list of conditions and the following disclaimer.
!
! 2. Redistributions in binary form must reproduce the above copyright
! notice, this list of conditions and the following disclaimer in the
! documentation and/or other materials provided with the distribution.
!
! 3. Neither the name of the copyright holder nor the names of its
! contributors may be used to endorse or promote products derived from
! this software without specific prior written permission.
!
! THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
! "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
! LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS
! FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE
! COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT,
! INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
! BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
! LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
! CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
! LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN
! ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
! POSSIBILITY OF SUCH DAMAGE.

module adjac
  private

  integer, parameter :: block_size = 4

  type, public :: adjac_double
     ! value + v * D[i]
     double precision :: value
     integer :: i
     double precision :: v
  end type adjac_double

  type, public :: adjac_complexan
     ! value + v * D[i]
     double complex :: value
     integer :: i
     double complex :: v
  end type adjac_complexan

  type, public :: adjac_complex
     type(adjac_double) :: re, im
  end type adjac_complex

  ! Differential binary tree
  !
  ! if (sum_map(1 + 2*(i-1)) .ne. 0) then
  !     D[i] = sum_mul(1+2*(i-1)) * D[sum_map(1+2*(i-1))] + sum_mul(2+2*(i-1)) * D[sum_map(2+2*(i-1))]
  ! else
  !     D[i] = D[i]
  !
  integer :: free_a = 1, free_q = 1
  integer, dimension(:), allocatable :: sum_map_a, sum_map_q
  double precision, dimension(:), allocatable :: sum_mul_a
  double complex, dimension(:), allocatable :: sum_mul_q

{{default pure = ""}}
{{default pure_elemental = ""}}

{{py:

def binops(name):
    chrs = ['i', 'd', 'z']
    ops = ['aa', 'bb', 'qq', 'ab', 'ba']
    for c in chrs:
        ops += ['a'+c, 'b'+c, 'q'+c, c+'a', c+'b', c+'q']
    oldops = list(ops)
    for suf in ['_1', '_2']:
        ops.extend([x + suf for x in oldops])
    s = "\n".join("     module procedure " + name + "_" + op for op in ops)
    return s.lstrip()

def elemental_versions(name, rtype, ops=None):
    if ops is None:
        chrs = ['i', 'd', 'z']
        ops = ['aa', 'bb', 'qq', 'ab', 'ba']
        for c in chrs:
            ops += ['a'+c, 'b'+c, 'q'+c, c+'a', c+'b', c+'q']

    binop_code = """
    function {name}_{op}_1(a, b) result(c)
      implicit none
      {typea}, dimension(:), intent(in) :: a
      {typeb}, dimension(size(a)), intent(in) :: b
      {typec}, dimension(size(a)) :: c
      integer :: i
      do i = 1, size(a)
        c(i) = {name}_{op}(a(i), b(i))
      end do
    end function {name}_{op}_1
    function {name}_{op}_2(a, b) result(c)
      implicit none
      {typea}, dimension(:,:), intent(in) :: a
      {typeb}, dimension(size(a,1),size(a,2)), intent(in) :: b
      {typec}, dimension(size(a,1),size(a,2)) :: c
      integer :: i, j
      do j = 1, size(a,2)
        do i = 1, size(a,1)
          c(i,j) = {name}_{op}(a(i,j), b(i,j))
        end do
      end do
    end function {name}_{op}_2
    """

    unop_code = """
    function {name}_{op}_1(a) result(c)
      implicit none
      {typea}, dimension(:), intent(in) :: a
      {typec}, dimension(size(a)) :: c
      integer :: i
      do i = 1, size(a)
        c(i) = {name}_{op}(a(i))
      end do
    end function {name}_{op}_1
    function {name}_{op}_2(a) result(c)
      implicit none
      {typea}, dimension(:,:), intent(in) :: a
      {typec}, dimension(size(a,1),size(a,2)) :: c
      integer :: i, j
      do j = 1, size(a,2)
        do i = 1, size(a,1)
          c(i,j) = {name}_{op}(a(i,j))
        end do
      end do
    end function {name}_{op}_2
    """

    assign_code = """
    subroutine {name}_{op}_1(c, a)
      implicit none
      {typea}, dimension(:), intent(in) :: a
      {typec}, dimension(size(a)), intent(out) :: c
      integer :: i
      do i = 1, size(a)
        call {name}_{op}(c(i), a(i))
      end do
    end subroutine {name}_{op}_1
    subroutine {name}_{op}_2(c, a)
      implicit none
      {typea}, dimension(:,:), intent(in) :: a
      {typec}, dimension(size(a,1),size(a,2)), intent(out) :: c
      integer :: i, j
      do j = 1, size(a,2)
        do i = 1, size(a,1)
          call {name}_{op}(c(i,j), a(i,j))
        end do
      end do
    end subroutine {name}_{op}_2
    """

    base_code = {'binop': binop_code, 'unop': unop_code, 'assign': assign_code}[rtype]
    types = {'a': 'type(adjac_double)', 
             'b': 'type(adjac_complex)',
             'q': 'type(adjac_complexan)',
             'i': 'integer',
             'd': 'double precision',
             'z': 'double complex'}

    s = ""
    typeb = ""
    for op in ops:
        if rtype == 'binop':
            typea = types[op[0]]
            typeb = types[op[1]]
            typec = typea if op[0] in 'abq' else typeb
            if typec == 'type(adjac_double)' and ('z' in op or 'b' in op): 
                typec = 'type(adjac_complex)'
        elif rtype == 'assign':
            typec = types[op[0]]
            typea = types[op[1]]
        else:
            typea = types[op[0]]
            typec = typea
        s += base_code.format(name=name, op=op, typea=typea, typeb=typeb, typec=typec)
    return s
}}

  public assignment(=)
  interface assignment(=)
     module procedure assign_ai, assign_ad
     module procedure assign_bi, assign_bd, assign_bz, assign_ba
     module procedure assign_qi, assign_qd, assign_qz

     module procedure assign_ai_1, assign_ad_1
     module procedure assign_bi_1, assign_bd_1, assign_bz_1, assign_ba_1
     module procedure assign_qi_1, assign_qd_1, assign_qz_1

     module procedure assign_ai_2, assign_ad_2
     module procedure assign_bi_2, assign_bd_2, assign_bz_2, assign_ba_2
     module procedure assign_qi_2, assign_qd_2, assign_qz_2
  end interface

  public operator(+)
  interface operator(+)
     {{binops("add")}}
  end interface

  public operator(-)
  interface operator(-)
     {{binops("sub")}}
     module procedure neg_a, neg_b, neg_q
  end interface

  public operator(*)
  interface operator(*)
     {{binops("mul")}}
  end interface operator(*)

  public operator(/)
  interface operator(/)
     {{binops("div")}}
  end interface operator(/)

  public operator(**)
  interface operator(**)
     module procedure pow_ai, pow_ad, pow_qi, pow_qd, pow_qz
     module procedure pow_ai_1, pow_ad_1, pow_qi_1, pow_qd_1, pow_qz_1
     module procedure pow_ai_2, pow_ad_2, pow_qi_2, pow_qd_2, pow_qz_2
  end interface operator(**)

  public matmul
  interface matmul
     module procedure matmul_aa, matmul_ai, matmul_ia, matmul_ad, matmul_da
     module procedure matmul_bb, matmul_bz, matmul_zb
     module procedure matmul_qq, matmul_qi, matmul_iq, matmul_qd, matmul_dq, matmul_qz, matmul_zq
  end interface matmul

  public dble
  interface dble
     module procedure dble_a, dble_b
     module procedure dble_a_1, dble_b_1
     module procedure dble_a_2, dble_b_2
  end interface dble

  public aimag
  interface aimag
     module procedure aimag_b
     module procedure aimag_b_1
     module procedure aimag_b_2
  end interface aimag

  public conjg
  interface conjg
     module procedure conjg_b
     module procedure conjg_b_1
     module procedure conjg_b_2
  end interface conjg

  public exp
  interface exp
     module procedure exp_a, exp_b, exp_q
     module procedure exp_a_1, exp_b_1, exp_q_1
     module procedure exp_a_2, exp_b_2, exp_q_2
  end interface exp

  public sin
  interface sin
     module procedure sin_a, sin_b, sin_q
     module procedure sin_a_1, sin_b_1, sin_q_1
     module procedure sin_a_2, sin_b_2, sin_q_2
  end interface sin

  public cos
  interface cos
     module procedure cos_a, cos_b, cos_q
     module procedure cos_a_1, cos_b_1, cos_q_1
     module procedure cos_a_2, cos_b_2, cos_q_2
  end interface cos

  public log
  interface log
     module procedure log_a, log_b, log_q
     module procedure log_a_1, log_b_1, log_q_1
     module procedure log_a_2, log_b_2, log_q_2
  end interface log

  interface adjac_set_independent
     module procedure set_independent_a, set_independent_q
     module procedure set_independent_many_a, set_independent_many_q
  end interface adjac_set_independent

  interface sum_taylor
     module procedure sum_taylor_a, sum_taylor_q
  end interface sum_taylor

  interface adjac_get_value
     module procedure get_value_one_a, get_value_one_q
     module procedure get_value_many_a, get_value_many_q
  end interface adjac_get_value

  interface adjac_get_dense_jacobian
     module procedure get_dense_jacobian_a, get_dense_jacobian_q
  end interface adjac_get_dense_jacobian

  interface adjac_get_csr_jacobian
     module procedure get_csr_jacobian_a, get_csr_jacobian_q
  end interface adjac_get_csr_jacobian

  interface adjac_get_coo_jacobian
     module procedure get_coo_jacobian_a, get_coo_jacobian_q
  end interface adjac_get_coo_jacobian

  interface adjac_get_nnz
     module procedure get_nnz_a, get_nnz_q
  end interface adjac_get_nnz

  public adjac_set_independent, adjac_get_value, &
       adjac_get_dense_jacobian, adjac_get_csr_jacobian, &
       adjac_get_coo_jacobian, adjac_get_nnz, &
       adjac_reset, adjac_free

contains

  subroutine adjac_reset()
    implicit none
    free_a = 1
    free_q = 1
  end subroutine adjac_reset

  subroutine adjac_free()
    implicit none
    free_a = 1
    free_q = 1
    if (allocated(sum_map_a)) then
       deallocate(sum_map_a)
       deallocate(sum_mul_a)
    end if
    if (allocated(sum_map_q)) then
       deallocate(sum_map_q)
       deallocate(sum_mul_q)
    end if
  end subroutine adjac_free

{{for TYPE, FTYPE, CHR, CAST, FTYPES2 in [('adjac_double', 'double precision', 'a', 'dble', [('integer', 'i'),
                                                                                             ('double precision', 'd')]), 
                                          ('adjac_complexan', 'double complex', 'q', 'dcmplx', [('integer', 'i'),
                                                                                                ('double precision', 'd'), 
                                                                                                ('double complex', 'z')])]}}

  subroutine get_free_variable_idx_{{CHR}}(j)
    implicit none
    integer, intent(out) :: j

    integer, dimension(:), allocatable :: itmp
    {{FTYPE}}, dimension(:), allocatable :: tmp
    integer :: sz

    if (.not.allocated(sum_map_{{CHR}})) then
       sz = 0
    else
       sz = size(sum_map_{{CHR}})
    end if

    if (sz < 2*free_{{CHR}} + 1) then
       ! Enlarge work space
       if (allocated(sum_map_{{CHR}})) then
          sz = sz + 2*free_{{CHR}} + 1
          allocate(itmp(sz), tmp(sz))
          itmp(1:size(sum_map_{{CHR}})) = sum_map_{{CHR}}(:)
          tmp(1:size(sum_mul_{{CHR}})) = sum_mul_{{CHR}}(:)
          call move_alloc(itmp, sum_map_{{CHR}})
          call move_alloc(tmp, sum_mul_{{CHR}})
       else
          allocate(sum_map_{{CHR}}(100), sum_mul_{{CHR}}(100))
       end if
    end if

    j = free_{{CHR}}
    free_{{CHR}} = free_{{CHR}} + 1
  end subroutine get_free_variable_idx_{{CHR}}

  subroutine set_independent_{{CHR}}(x, xval, j)
    implicit none
    type({{TYPE}}), intent(out) :: x
    {{FTYPE}}, intent(in) :: xval
    integer, intent(in) :: j

    x%value = xval
    call get_free_variable_idx_{{CHR}}(x%i)
    x%v = 1

    ! Independent variable
    sum_map_{{CHR}}(1 + 2*(x%i-1)) = 0
    sum_map_{{CHR}}(2 + 2*(x%i-1)) = j
  end subroutine set_independent_{{CHR}}

  subroutine sum_taylor_{{CHR}}(alpha, beta, a, b, c)
    ! dc := alpha*da + beta*db
    implicit none
    {{FTYPE}}, intent(in) :: alpha, beta
    type({{TYPE}}), intent(in) :: a, b
    type({{TYPE}}), intent(inout) :: c

    if (a%v == 0 .or. a%i == 0) then
       c%v = beta * b%v
       c%i = b%i
    else if (b%v == 0 .or. b%i == 0) then
       c%v = alpha * a%v
       c%i = a%i
    else if (a%i == b%i) then
       c%v = alpha * a%v + beta * b%v
       c%i = a%i
    else
       call get_free_variable_idx_{{CHR}}(c%i)
       c%v = 1
       sum_map_{{CHR}}(1 + 2*(c%i-1)) = a%i
       sum_map_{{CHR}}(2 + 2*(c%i-1)) = b%i
       sum_mul_{{CHR}}(1 + 2*(c%i-1)) = alpha * a%v
       sum_mul_{{CHR}}(2 + 2*(c%i-1)) = beta * b%v
    end if
  end subroutine sum_taylor_{{CHR}}

  subroutine set_independent_many_{{CHR}}(x, xval)
    implicit none
    type({{TYPE}}), dimension(:), intent(inout) :: x
    {{FTYPE}}, dimension(size(x)), intent(in) :: xval

    integer :: j

    do j = 1, size(x,1)
       call set_independent_{{CHR}}(x(j), xval(j), j)
    end do
  end subroutine set_independent_many_{{CHR}}

  subroutine get_value_one_{{CHR}}(y, val)
    implicit none
    type({{TYPE}}), intent(in) :: y
    {{FTYPE}}, intent(out) :: val
    val = y%value
  end subroutine get_value_one_{{CHR}}

  subroutine get_value_many_{{CHR}}(y, val)
    implicit none
    type({{TYPE}}), dimension(:), intent(in) :: y
    {{FTYPE}}, dimension(size(y,1)), intent(out) :: val
    integer :: j
    do j = 1, size(val,1)
       val(j) = y(j)%value
    end do
  end subroutine get_value_many_{{CHR}}

{{py:
def walk_tape(ftype, chr, assign, accumulate=None, clear=None):
    if accumulate is None:
        accumulate = """
        work(:,ia) = work(:,ia) + sum_mul_{chr}(1+2*(j-1)) * work(:,j)
        work(:,ib) = work(:,ib) + sum_mul_{chr}(2+2*(j-1)) * work(:,j)
        """.format(chr=chr)
    if clear is None:
        clear = "work(:,j) = 0"
    return """
    ! Traverse the tape
    do j = i, 1, -1
       if (iwork(j) .ne. 0) then
          ia = sum_map_{chr}(1+2*(j-1))
          ib = sum_map_{chr}(2+2*(j-1))

          if (ia == 0) then
             {assign}
          else
             {accumulate}
             iwork(ia) = 1
             iwork(ib) = 1
          end if
          iwork(j) = 0
          {clear}
       end if
    end do
""".format(ftype=ftype, chr=chr, assign=assign, accumulate=accumulate, clear=clear)
}}

  function get_nnz_{{CHR}}(y) result(nnz)
    type({{TYPE}}), dimension(:), intent(in) :: y
    integer :: nnz

    integer, dimension(free_{{CHR}}) :: iwork
    integer :: k, j, ia, ib

    nnz = 0
    iwork = 0
    do k = 1, size(y,1)
       i = y(k)%i
       iwork(i) = 1
       {{walk_tape(FTYPE, CHR, 
                   "nnz = nnz + 1", 
                   "", "")}}
    end do
  end function get_nnz_{{CHR}}

  subroutine get_row_nnz_{{CHR}}(y, nnz)
    type({{TYPE}}), dimension(:), intent(in) :: y
    integer, dimension(*), intent(out) :: nnz

    integer, dimension(free_{{CHR}}) :: iwork
    integer :: k, j, ia, ib

    nnz(1:size(y,1)) = 0
    iwork = 0
    do k = 1, size(y,1)
       i = y(k)%i
       iwork(i) = 1
       {{walk_tape(FTYPE, CHR, 
                   "nnz(k) = nnz(k) + 1", 
                   "", "")}}
    end do
  end subroutine get_row_nnz_{{CHR}}

  subroutine get_dense_jacobian_{{CHR}}(y, jac_dense)
    implicit none
    type({{TYPE}}), dimension(:), intent(inout) :: y
    {{FTYPE}}, dimension(:,:), intent(out) :: jac_dense

    {{FTYPE}}, dimension(block_size,free_{{CHR}}) :: work
    integer, dimension(free_{{CHR}}) :: iwork
    integer :: i, k, j, ia, ib, kmin, kmax
    {{FTYPE}} :: v

    jac_dense = 0

    work = 0
    iwork = 0

    do kmin = 1, size(y,1), block_size
       kmax = min(kmin + block_size - 1, size(y,1))

       i = y(kmin)%i
       do k = kmin, kmax
          i = max(i, y(k)%i)
       end do

       do k = kmin, kmax
          work(k-kmin+1, y(k)%i) = y(k)%v
          iwork(y(k)%i) = 1
       end do

       {{walk_tape(FTYPE, CHR, "jac_dense(kmin:kmax,ib) = work(1:(kmax-kmin+1),j)")}}
    end do
  end subroutine get_dense_jacobian_{{CHR}}

  subroutine get_coo_jacobian_{{CHR}}(y, jac_val, jac_i, jac_j)
    implicit none
    type({{TYPE}}), dimension(:), intent(inout) :: y
    {{FTYPE}}, dimension(:), intent(out) :: jac_val
    integer, dimension(:), intent(out) :: jac_i, jac_j

    {{FTYPE}}, dimension(block_size,free_{{CHR}}) :: work
    integer, dimension(free_{{CHR}}) :: iwork
    integer :: i, kmin, kmax, k, j, ia, ib, pos
    {{FTYPE}} :: v

    pos = 1
    work = 0
    iwork = 0

    do kmin = 1, size(y,1), block_size
       kmax = min(kmin + block_size - 1, size(y,1))

       i = y(kmin)%i
       do k = kmin, kmax
          i = max(i, y(k)%i)
          work(k-kmin+1, y(k)%i) = y(k)%v
          iwork(y(k)%i) = 1
       end do

       {{walk_tape(FTYPE, CHR, """
            do k = kmin, kmax
               if (work(k-kmin+1,j).ne.0) then
                   jac_i(pos) = k
                   jac_j(pos) = ib
                   jac_val(pos) = work(k-kmin+1,j)
                   pos = pos + 1
                end if
            end do
        """)}}
    end do
  end subroutine get_coo_jacobian_{{CHR}}

  subroutine get_csr_jacobian_{{CHR}}(y, jac_val, jac_indices, jac_indptr)
    implicit none
    type({{TYPE}}), dimension(:), intent(inout) :: y
    {{FTYPE}}, dimension(*), intent(out) :: jac_val
    integer, dimension(*), intent(out) :: jac_indices, jac_indptr

    {{FTYPE}}, dimension(block_size,free_{{CHR}}) :: work
    integer, dimension(free_{{CHR}}) :: iwork
    integer, dimension(block_size) :: pos
    integer :: i, j, k, kmax, kmin, ia, ib
    {{FTYPE}} :: v

    jac_indptr(1) = 1
    call get_row_nnz_{{CHR}}(y, jac_indptr(2))
    do k = 2, size(y,1)+1
       jac_indptr(k) = jac_indptr(k) + jac_indptr(k-1)
    end do

    work = 0
    iwork = 0

    do kmin = 1, size(y,1), block_size
       kmax = min(kmin + block_size - 1, size(y,1))

       i = y(kmin)%i
       do k = kmin, kmax
          i = max(i, y(k)%i)
          work(k-kmin+1, y(k)%i) = y(k)%v
          iwork(y(k)%i) = 1
       end do

       pos = 1

       {{walk_tape(FTYPE, CHR, """
            do k = kmin, kmax
               if (work(k-kmin+1,j).ne.0) then
                  jac_indices(jac_indptr(k+1) - pos(k-kmin+1)) = ib
                  jac_val(jac_indptr(k+1) - pos(k-kmin+1)) = work(k-kmin+1,j)
                  pos(k-kmin+1) = pos(k-kmin+1) + 1
                end if
            end do
        """)}}
    end do
  end subroutine get_csr_jacobian_{{CHR}}

  !--------------------------------------------------------------------------
  ! Overloaded operators
  !--------------------------------------------------------------------------

  !!
  !! assignment(=)
  !!

  {{for FTYPE2, CHR2 in FTYPES2}}
  {{pure_elemental}} subroutine assign_{{CHR}}{{CHR2}}(x, y)
    implicit none
    type({{TYPE}}), intent(out) :: x
    {{FTYPE2}}, intent(in) :: y
    x%value = y
    x%i = 0
    x%v = 0
  end subroutine assign_{{CHR}}{{CHR2}}
  {{endfor}}

  {{if TYPE == "adjac_double"}}
  {{for FTYPE2, CHR2 in FTYPES2}}
  {{pure_elemental}} subroutine assign_b{{CHR2}}(x, y)
    implicit none
    type(adjac_complex), intent(out) :: x
    {{FTYPE2}}, intent(in) :: y
    x%re = dble(y)
    x%im = 0d0
  end subroutine assign_b{{CHR2}}
  {{endfor}}

  {{pure_elemental}} subroutine assign_bz(x, y)
    implicit none
    type(adjac_complex), intent(out) :: x
    double complex, intent(in) :: y
    x%re = dble(y)
    x%im = aimag(y)
  end subroutine assign_bz

  {{pure_elemental}} subroutine assign_ba(x, y)
    implicit none
    type(adjac_complex), intent(out) :: x
    type({{TYPE}}), intent(in) :: y
    x%re = y
    x%im = 0d0
  end subroutine assign_ba
  {{endif}}

  !!
  !! operator(+)
  !!

  ! X + Y = x + y + (x_j - y_j) dj

  {{pure_elemental}} function add_{{CHR}}{{CHR}}(x, y) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x, y
    type({{TYPE}}) :: z

    z%value = x%value + y%value
    call sum_taylor({{CAST}}(1d0), {{CAST}}(1d0), x, y, z)
  end function add_{{CHR}}{{CHR}}

  {{for FTYPE2, CHR2 in FTYPES2}}
  {{pure_elemental}} function add_{{CHR}}{{CHR2}}(x, y) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    {{FTYPE2}}, intent(in) :: y
    type({{TYPE}}) :: z
    z%value = x%value + y
    z%i = x%i
    z%v = x%v
  end function add_{{CHR}}{{CHR2}}

  {{pure_elemental}} function add_{{CHR2}}{{CHR}}(x, y) result(z)
    implicit none
    {{FTYPE2}}, intent(in) :: x
    type({{TYPE}}), intent(in) :: y
    type({{TYPE}}) :: z
    z = y + x
  end function add_{{CHR2}}{{CHR}}
  {{endfor}}

  {{if TYPE == "adjac_double"}}
  {{pure_elemental}} function add_az(x, y) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    double complex, intent(in) :: y
    type(adjac_complex) :: z
    z%re = x + dble(y)
    z%im = aimag(y)
  end function add_az

  {{pure_elemental}} function add_za(x, y) result(z)
    implicit none
    double complex, intent(in) :: x
    type({{TYPE}}), intent(in) :: y
    type(adjac_complex) :: z
    z%re = dble(x) + y
    z%im = aimag(x)
  end function add_za

  {{pure_elemental}} function add_bb(x, y) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    type(adjac_complex), intent(in) :: y
    type(adjac_complex) :: z
    z%re = x%re + y%re
    z%im = x%im + y%im
  end function add_bb

  {{pure_elemental}} function add_bz(x, y) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    double complex, intent(in) :: y
    type(adjac_complex) :: z
    z%re = x%re + dble(y)
    z%im = x%im + aimag(y)
  end function add_bz

  {{pure_elemental}} function add_zb(x, y) result(z)
    implicit none
    double complex, intent(in) :: x
    type(adjac_complex), intent(in) :: y
    type(adjac_complex) :: z
    z%re = dble(x) + y%re
    z%im = aimag(x) + y%im
  end function add_zb

  {{pure_elemental}} function add_ba(x, y) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    type({{TYPE}}), intent(in) :: y
    type(adjac_complex) :: z
    z%re = x%re + y
    z%im = x%im
  end function add_ba

  {{pure_elemental}} function add_ab(x, y) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    type(adjac_complex), intent(in) :: y
    type(adjac_complex) :: z
    z%re = x + y%re
    z%im = y%im
  end function add_ab

  {{for FTYPE2, CHR2 in FTYPES2}}
  {{pure_elemental}} function add_b{{CHR2}}(x, y) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    {{FTYPE2}}, intent(in) :: y
    type(adjac_complex) :: z
    z = x + dcmplx(y)
  end function add_b{{CHR2}}

  {{pure_elemental}} function add_{{CHR2}}b(x, y) result(z)
    implicit none
    {{FTYPE2}}, intent(in) :: x
    type(adjac_complex), intent(in) :: y
    type(adjac_complex) :: z
    z = dcmplx(x) + y
  end function add_{{CHR2}}b
  {{endfor}}

  {{endif}}

  !!
  !! operator(-)
  !!

  ! X - Y = x - y + (x_j - y_j) dj

  {{pure_elemental}} function sub_{{CHR}}{{CHR}}(x, y) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x, y
    type({{TYPE}}) :: z
    z%value = x%value - y%value
    call sum_taylor({{CAST}}(1d0), {{CAST}}(-1d0), x, y, z)
  end function sub_{{CHR}}{{CHR}}

  {{for FTYPE2, CHR2 in FTYPES2}}
  {{pure_elemental}} function sub_{{CHR}}{{CHR2}}(x, y) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    {{FTYPE2}}, intent(in) :: y
    type({{TYPE}}) :: z
    z%value = x%value - y
    z%v = x%v
    z%i = x%i
  end function sub_{{CHR}}{{CHR2}}

  {{pure_elemental}} function sub_{{CHR2}}{{CHR}}(x, y) result(z)
    implicit none
    {{FTYPE2}}, intent(in) :: x
    type({{TYPE}}), intent(in) :: y
    type({{TYPE}}) :: z
    z%value = x - y%value
    z%i = y%i
    z%v = -y%v
  end function sub_{{CHR2}}{{CHR}}
  {{endfor}}

  {{if TYPE == "adjac_double"}}
  {{pure_elemental}} function sub_az(x, y) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    double complex, intent(in) :: y
    type(adjac_complex) :: z
    z%re = x - dble(y)
    z%im = -aimag(y)
  end function sub_az

  {{pure_elemental}} function sub_za(x, y) result(z)
    implicit none
    double complex, intent(in) :: x
    type({{TYPE}}), intent(in) :: y
    type(adjac_complex) :: z
    z%re = dble(x) - y
    z%im = aimag(x)
  end function sub_za

  {{pure_elemental}} function sub_bb(x, y) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    type(adjac_complex), intent(in) :: y
    type(adjac_complex) :: z
    z%re = x%re - y%re
    z%im = x%im - y%im
  end function sub_bb

  {{pure_elemental}} function sub_bz(x, y) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    double complex, intent(in) :: y
    type(adjac_complex) :: z
    z%re = x%re - dble(y)
    z%im = x%im - aimag(y)
  end function sub_bz

  {{pure_elemental}} function sub_zb(x, y) result(z)
    implicit none
    double complex, intent(in) :: x
    type(adjac_complex), intent(in) :: y
    type(adjac_complex) :: z
    z%re = dble(x) - y%re
    z%im = aimag(x) - y%im
  end function sub_zb

  {{pure_elemental}} function sub_ba(x, y) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    type({{TYPE}}), intent(in) :: y
    type(adjac_complex) :: z
    z%re = x%re - y
    z%im = x%im
  end function sub_ba

  {{pure_elemental}} function sub_ab(x, y) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    type(adjac_complex), intent(in) :: y
    type(adjac_complex) :: z
    z%re = x - y%re
    z%im = -y%im
  end function sub_ab

  {{for FTYPE2, CHR2 in FTYPES2}}
  {{pure_elemental}} function sub_b{{CHR2}}(x, y) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    {{FTYPE2}}, intent(in) :: y
    type(adjac_complex) :: z
    z = x - dcmplx(y)
  end function sub_b{{CHR2}}

  {{pure_elemental}} function sub_{{CHR2}}b(x, y) result(z)
    implicit none
    {{FTYPE2}}, intent(in) :: x
    type(adjac_complex), intent(in) :: y
    type(adjac_complex) :: z
    z = dcmplx(x) - y
  end function sub_{{CHR2}}b
  {{endfor}}
  {{endif}}

  !!
  !! operator(-), unary
  !!

  {{pure_elemental}} function neg_{{CHR}}(x) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    type({{TYPE}}) :: z
    z = 0d0 - x
  end function neg_{{CHR}}

  {{if TYPE == "adjac_double"}}
  {{pure_elemental}} function neg_b(x) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    type(adjac_complex) :: z
    z = (0d0,0d0) - z
  end function neg_b
  {{endif}}

  !!
  !! operator(*)
  !!

  ! X*Y = x*y + (x y_j + y x_j) dj

  {{pure_elemental}} function mul_{{CHR}}{{CHR}}(x, y) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x, y
    type({{TYPE}}) :: z

    z%value = x%value * y%value
    call sum_taylor(y%value, x%value, x, y, z)
  end function mul_{{CHR}}{{CHR}}

  {{for FTYPE2, CHR2 in FTYPES2}}
  {{pure_elemental}} function mul_{{CHR}}{{CHR2}}(x, y) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    {{FTYPE2}}, intent(in) :: y
    type({{TYPE}}) :: z
    z%value = x%value * y
    z%i = x%i
    z%v = x%v * y
  end function mul_{{CHR}}{{CHR2}}

  {{pure_elemental}} function mul_{{CHR2}}{{CHR}}(x, y) result(z)
    implicit none
    {{FTYPE2}}, intent(in) :: x
    type({{TYPE}}), intent(in) :: y
    type({{TYPE}}) :: z
    z = y * x
  end function mul_{{CHR2}}{{CHR}}
  {{endfor}}

  {{if TYPE == "adjac_double"}}
  {{pure_elemental}} function mul_az(x, y) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    double complex, intent(in) :: y
    type(adjac_complex) :: z
    z%re = x * dble(y)
    z%im = x * aimag(y)
  end function mul_az

  {{pure_elemental}} function mul_za(x, y) result(z)
    implicit none
    double complex, intent(in) :: x
    type({{TYPE}}), intent(in) :: y
    type(adjac_complex) :: z
    z%re = dble(x) * y
    z%im = aimag(x) * y
  end function mul_za

  {{pure_elemental}} function mul_bb(x, y) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    type(adjac_complex), intent(in) :: y
    type(adjac_complex) :: z
    z%re = x%re * y%re - x%im * y%im
    z%im = x%re * y%im + x%im * y%re
  end function mul_bb

  {{pure_elemental}} function mul_bz(x, y) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    double complex, intent(in) :: y
    type(adjac_complex) :: z
    z%re = x%re * dble(y) - x%im * aimag(y)
    z%im = x%re * aimag(y) + x%im * dble(y)
  end function mul_bz

  {{pure_elemental}} function mul_zb(x, y) result(z)
    implicit none
    double complex, intent(in) :: x
    type(adjac_complex), intent(in) :: y
    type(adjac_complex) :: z
    z%re = dble(x) * y%re - aimag(x) * y%im
    z%im = dble(x) * y%im + aimag(x) * y%re
  end function mul_zb

  {{pure_elemental}} function mul_ba(x, y) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    type({{TYPE}}), intent(in) :: y
    type(adjac_complex) :: z
    z%re = x%re * y
    z%im = x%im * y
  end function mul_ba

  {{pure_elemental}} function mul_ab(x, y) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    type(adjac_complex), intent(in) :: y
    type(adjac_complex) :: z
    z%re = x * y%re
    z%im = x * y%im
  end function mul_ab

  {{for FTYPE2, CHR2 in FTYPES2}}
  {{pure_elemental}} function mul_b{{CHR2}}(x, y) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    {{FTYPE2}}, intent(in) :: y
    type(adjac_complex) :: z
    z = x * dcmplx(y)
  end function mul_b{{CHR2}}

  {{pure_elemental}} function mul_{{CHR2}}b(x, y) result(z)
    implicit none
    {{FTYPE2}}, intent(in) :: x
    type(adjac_complex), intent(in) :: y
    type(adjac_complex) :: z
    z = dcmplx(x) * y
  end function mul_{{CHR2}}b
  {{endfor}}
  {{endif}}

  !!
  !! operator(/)
  !!

  ! X/Y = x/y + (x_j/y - x y_j/y**2) dj

  {{pure_elemental}} function div_{{CHR}}{{CHR}}(x, y) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x, y
    type({{TYPE}}) :: z
    z%value = x%value / y%value
    call sum_taylor(1d0/y%value, -x%value/(y%value**2), x, y, z)
  end function div_{{CHR}}{{CHR}}

  {{for FTYPE2, CHR2 in FTYPES2}}
  {{pure_elemental}} function div_{{CHR}}{{CHR2}}(x, y) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    {{FTYPE2}}, intent(in) :: y
    type({{TYPE}}) :: z
    z = (1d0 / y) * x
  end function div_{{CHR}}{{CHR2}}

  {{pure_elemental}} function div_{{CHR2}}{{CHR}}(x, y) result(z)
    implicit none
    {{FTYPE2}}, intent(in) :: x
    type({{TYPE}}), intent(in) :: y
    type({{TYPE}}) :: z
    z = (-x / (y%value**2)) * y
    z%value = x / y%value
  end function div_{{CHR2}}{{CHR}}
  {{endfor}}

  {{if TYPE == "adjac_double"}}
  {{pure_elemental}} function div_az(x, y) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    double complex, intent(in) :: y
    type(adjac_complex) :: z
    z = x * conjg(y) / (dble(y)*dble(y) + aimag(y)*aimag(y))
  end function div_az

  {{pure_elemental}} function div_za(x, y) result(z)
    implicit none
    double complex, intent(in) :: x
    type({{TYPE}}), intent(in) :: y
    type(adjac_complex) :: z
    z%re = dble(x) / y
    z%im = aimag(x) / y
  end function div_za

  {{pure_elemental}} function div_bb(x, y) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    type(adjac_complex), intent(in) :: y
    type(adjac_complex) :: z
    z = x * conjg(y) / (dble(y)*dble(y) + aimag(y)*aimag(y))
  end function div_bb

  {{pure_elemental}} function div_bz(x, y) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    double complex, intent(in) :: y
    type(adjac_complex) :: z
    z = x * conjg(y) / (dble(y)*dble(y) + aimag(y)*aimag(y))
  end function div_bz

  {{pure_elemental}} function div_zb(x, y) result(z)
    implicit none
    double complex, intent(in) :: x
    type(adjac_complex), intent(in) :: y
    type(adjac_complex) :: z
    z = x * conjg(y) / (dble(y)*dble(y) + aimag(y)*aimag(y))
  end function div_zb

  {{pure_elemental}} function div_ba(x, y) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    type({{TYPE}}), intent(in) :: y
    type(adjac_complex) :: z
    z%re = x%re / y
    z%im = x%im / y
  end function div_ba

  {{pure_elemental}} function div_ab(x, y) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    type(adjac_complex), intent(in) :: y
    type(adjac_complex) :: z
    z = x * conjg(y) / (dble(y)*dble(y) + aimag(y)*aimag(y))
  end function div_ab

  {{for FTYPE2, CHR2 in FTYPES2}}
  {{pure_elemental}} function div_b{{CHR2}}(x, y) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    {{FTYPE2}}, intent(in) :: y
    type(adjac_complex) :: z
    z%re = x%re / y
    z%im = x%im / y
  end function div_b{{CHR2}}

  {{pure_elemental}} function div_{{CHR2}}b(x, y) result(z)
    implicit none
    {{FTYPE2}}, intent(in) :: x
    type(adjac_complex), intent(in) :: y
    type(adjac_complex) :: z
    z = dcmplx(x) / y
  end function div_{{CHR2}}b
  {{endfor}}
  {{endif}}

  !!
  !! operator(**)
  !!

  {{for FTYPE2, CHR2 in FTYPES2}}
  {{pure_elemental}} function pow_{{CHR}}{{CHR2}}(x, y) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    {{FTYPE2}}, intent(in) :: y
    type({{TYPE}}) :: z
    z = exp(y * log(x))
  end function pow_{{CHR}}{{CHR2}}
  {{endfor}}

  !!
  !! matmul
  !!

{{def matmulcode}}
    integer i, j, k

    if (size(x,2) .ne. size(y,1)) then
       write(*,*) 'invalid array sizes in matmul'
       stop
    end if

    do j = 1, size(y,2)
       do i = 1, size(x,1)
          z(i,j) = x(i,1)*y(1,j)
          do k = 2, size(x,2)
             z(i,j) = z(i,j) + x(i,k)*y(k,j)
          end do
       end do
    end do
{{enddef}}

  function matmul_{{CHR}}{{CHR}}(x, y) result(z)
    implicit none
    type({{TYPE}}), dimension(:,:), intent(in) :: x, y
    type({{TYPE}}), dimension(size(x,1),size(y,2)) :: z
    {{matmulcode}}
  end function matmul_{{CHR}}{{CHR}}

  {{for FTYPE2, CHR2 in FTYPES2}}
  function matmul_{{CHR}}{{CHR2}}(x, y) result(z)
    implicit none
    type({{TYPE}}), dimension(:,:), intent(in) :: x
    {{FTYPE2}}, dimension(:,:), intent(in) :: y
    type({{TYPE}}), dimension(size(x,1),size(y,2)) :: z
    {{matmulcode}}
  end function matmul_{{CHR}}{{CHR2}}

  function matmul_{{CHR2}}{{CHR}}(x, y) result(z)
    implicit none
    {{FTYPE2}}, dimension(:,:), intent(in) :: x
    type({{TYPE}}), dimension(:,:), intent(in) :: y
    type({{TYPE}}), dimension(size(x,1),size(y,2)) :: z
    {{matmulcode}}
  end function matmul_{{CHR2}}{{CHR}}
  {{endfor}}

  {{if TYPE == "adjac_double"}}
  function matmul_bb(x, y) result(z)
    implicit none
    type(adjac_complex), dimension(:,:), intent(in) :: x, y
    type(adjac_complex), dimension(size(x,1),size(y,2)) :: z
    {{matmulcode}}
  end function matmul_bb

  function matmul_bz(x, y) result(z)
    implicit none
    type(adjac_complex), dimension(:,:), intent(in) :: x
    double complex, dimension(:,:), intent(in) :: y
    type(adjac_complex), dimension(size(x,1),size(y,2)) :: z
    {{matmulcode}}
  end function matmul_bz

  function matmul_zb(x, y) result(z)
    implicit none
    double complex, dimension(:,:), intent(in) :: x
    type(adjac_complex), dimension(:,:), intent(in) :: y
    type(adjac_complex), dimension(size(x,1),size(y,2)) :: z
    {{matmulcode}}
  end function matmul_zb
  {{endif}}

  !!
  !! dble
  !!

  {{if TYPE == "adjac_double"}}
  {{pure_elemental}} function dble_a(x) result(z)
    implicit none
    type(adjac_double), intent(in) :: x
    type(adjac_double) :: z
    z = x
  end function dble_a

  {{pure_elemental}} function dble_b(x) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    type(adjac_double) :: z
    z = x%re
  end function dble_b
  {{endif}}

  !!
  !! aimag
  !!

  {{if TYPE == "adjac_double"}}
  {{pure_elemental}} function aimag_b(x) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    type(adjac_double) :: z
    z = x%im
  end function aimag_b
  {{endif}}

  !!
  !! conjg
  !!

  {{if TYPE == "adjac_double"}}
  {{pure_elemental}} function conjg_b(x) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    type(adjac_complex) :: z
    z%re = x%re
    z%im = -x%im
  end function conjg_b
  {{endif}}

  !!
  !! exp
  !!

  {{pure_elemental}} function exp_{{CHR}}(x) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    type({{TYPE}}) :: z
    {{FTYPE}} :: v, dv
    v = exp(x%value)
    dv = v
    z = dv*x
    z%value = v
  end function exp_{{CHR}}

  {{if TYPE == "adjac_double"}}
  {{pure_elemental}} function exp_b(x) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    type(adjac_complex) :: z
    double complex :: v, dv
    v = exp(dcmplx(x%re%value, x%im%value))
    dv = v
    z = dv*x
    z%re%value = dble(v)
    z%im%value = aimag(v)
  end function exp_b
  {{endif}}

  !!
  !! sin
  !!

  {{pure_elemental}} function sin_{{CHR}}(x) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    type({{TYPE}}) :: z
    {{FTYPE}} :: v, dv
    v = sin(x%value)
    dv = cos(x%value)
    z = dv*x
    z%value = v
  end function sin_{{CHR}}

  {{if TYPE == "adjac_double"}}
  {{pure_elemental}} function sin_b(x) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    type(adjac_complex) :: z
    double complex :: v, dv
    v = sin(dcmplx(x%re%value, x%im%value))
    dv = cos(dcmplx(x%re%value, x%im%value))
    z = dv*x
    z%re%value = dble(v)
    z%im%value = aimag(v)
  end function sin_b
  {{endif}}

  !!
  !! cos
  !!

  {{pure_elemental}} function cos_{{CHR}}(x) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    type({{TYPE}}) :: z
    {{FTYPE}} :: v, dv
    v = cos(x%value)
    dv = -sin(x%value)
    z = dv*x
    z%value = v
  end function cos_{{CHR}}

  {{if TYPE == "adjac_double"}}
  {{pure_elemental}} function cos_b(x) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    type(adjac_complex) :: z
    double complex :: v, dv
    v = cos(dcmplx(x%re%value, x%im%value))
    dv = -sin(dcmplx(x%re%value, x%im%value))
    z = dv*x
    z%re%value = dble(v)
    z%im%value = aimag(v)
  end function cos_b
  {{endif}}

  !!
  !! log
  !!

  {{pure_elemental}} function log_{{CHR}}(x) result(z)
    implicit none
    type({{TYPE}}), intent(in) :: x
    type({{TYPE}}) :: z
    {{FTYPE}} :: v, dv
    v = log(x%value)
    dv = 1d0/x%value
    z = dv*x
    z%value = v
  end function log_{{CHR}}

  {{if TYPE == "adjac_double"}}
  {{pure_elemental}} function log_b(x) result(z)
    implicit none
    type(adjac_complex), intent(in) :: x
    type(adjac_complex) :: z
    double complex :: v, dv
    v = log(dcmplx(x%re%value, x%im%value))
    dv = 1d0/dcmplx(x%re%value, x%im%value)
    z = dv*x
    z%re%value = dble(v)
    z%im%value = aimag(v)
  end function log_b
  {{endif}}

{{endfor}}

  {{elemental_versions('assign', 'assign', ops='ai ad bi bd bz ba qi qd qz'.split())}}
  {{elemental_versions('add', 'binop')}}
  {{elemental_versions('sub', 'binop')}}
  {{elemental_versions('mul', 'binop')}}
  {{elemental_versions('div', 'binop')}}
  {{elemental_versions('pow', 'binop', ops=['ai', 'ad', 'qi', 'qd', 'qz'])}}
  {{elemental_versions('dble', 'unop', ops=['a', 'b'])}}
  {{elemental_versions('aimag', 'unop', ops=['b'])}}
  {{elemental_versions('conjg', 'unop', ops=['b'])}}
  {{elemental_versions('exp', 'unop', ops=['a', 'b', 'q'])}}
  {{elemental_versions('sin', 'unop', ops=['a', 'b', 'q'])}}
  {{elemental_versions('cos', 'unop', ops=['a', 'b', 'q'])}}
  {{elemental_versions('log', 'unop', ops=['a', 'b', 'q'])}}

end module adjac
